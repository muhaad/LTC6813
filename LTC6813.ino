#include <SPI.h>
#include <cmath>
#include <string>

#include "COMMANDS.h"
#include "CONFIGURE.h"
#include "HEADER.h"
#include "LUTS.h"

#include "Watchdog_t4.h"
#include <FlexCAN_T4.h>
#include <SPI.h>
#include <algorithm>

#include <SD.h>

//CAN pins
#define CRX3 23
#define CTX3 22
#define STBY 21     //CAN Transceiver Standby

//SD card pins
const int chipSelect = BUILTIN_SDCARD;

//SPI pins
#define CS 10   //chip select pin isoSPI
#define CS2 38   //3nd chip select pin isoSPI
#define CS1 0   //chip select for ADC

//counters
unsigned int start_time = millis();
unsigned int sense_watchdog_timer;   //senseboard watchdog timer. Sense boards will go to sleep after 2 seconds if no valid command with correct PEC is sent from master. 
bool new_voltage = false;
bool new_temp = false;

//flags
int wire_cut = 0;
bool memory_fault = 0;
bool comms_fault = 0;
bool curr_sense_fault = 0;
bool watchdog_callback = 0;
bool watchdog_reset = 0;
bool charger_fault = 0;

bool CHG_EN = 0; //0: enable charging, 1: disable charging

FlexCAN_T4<CAN1, RX_SIZE_256, TX_SIZE_16> can;      //    https://github.com/tonton81/FlexCAN_T4/tree/master

WDT_T4<WDT1> wdt;     //watchdog 1 holds output pin low until power-on-reset. This is desired for a shutdown circuit

// // Shared variables
float current = 0;

float current_offset = 0;

//state of charge
float soc = 0.0;

// data.csv enumeration
int data_file_num = 0;

//inverter voltage read fromc CAN
float inv_voltage = 0;

//LTC6813 minimum supply voltage is 16V
float cell_voltage[num_boards][num_cells];     //most recent cell voltages
float pack_voltage = 0;                        //sum of cell voltages
float cell_temp[num_boards][9];                //most recent cell temperatures. Contans raw voltage data for the duration of open wire checks
float die_temps[num_boards];                   //most recent sense board LTC6813 die temps

//sense board flags
float GPIO_open_wire[num_boards][9];
bool overvoltage_flag[18];
bool undervoltage_flag[18];

//measurement buffers
unsigned int time_buffer[SD_interval];
float voltage_buffer[SD_interval/volt_interval][num_boards][num_cells];
float temp_buffer[SD_interval/temp_interval][num_boards][9]; 
float current_buffer[SD_interval/current_interval];



void setup() {
  //open shutdown circuit
  pinMode(20, OUTPUT);
  digitalWrite(20, LOW);

  delay(5000);      //delay upon startup should be use to make it easier to recover the teensy when runtime errors occurs

  //start timers
  sense_watchdog_timer = start_time - 5000; //initial sense_watchdog timer with expired watchdog time (T - 2000 milliseconds)
  
  Serial.begin(9600);
  Serial.println("startup");
  Serial.print("Start Time: "); Serial.println(start_time);

  //SPI (isoSPI)
  pinMode(CS,OUTPUT);
  digitalWrite(CS, HIGH);
  SPI.begin();
  SPI.beginTransaction(SPISettings(1000000, MSBFIRST, SPI_MODE0));

  //SPI1 (ADC)
  pinMode(CS1, OUTPUT);
  digitalWrite(CS1, HIGH);
  SPI1.begin();
  SPI1.beginTransaction(SPISettings(1000000, MSBFIRST, SPI_MODE1));

  //CAN
  pinMode(CRX3, INPUT);
  pinMode(CTX3, OUTPUT);
  pinMode(STBY, OUTPUT);
  can.begin();
  can.setBaudRate(250000);
  can.setMaxMB(3);        //number of CAN message mailboxes
  digitalWrite(STBY, LOW);
  //    https://github.com/tonton81/FlexCAN_T4/blob/master/examples/mailbox_filtering_example_with_interrupts/mailbox_filtering_example_with_interrupts.ino
  // Mailboxes must be configured for all messages - both TX and RX
  can.setMB((FLEXCAN_MAILBOX)0,RX,STD);   //Standard mailbox for Inverter ID
  can.setMB((FLEXCAN_MAILBOX)1,RX,EXT);   //Extended id for charger
  can.setMB((FLEXCAN_MAILBOX)2,TX,EXT);   //BMS TX -> charger id
  can.setMBFilter(MB0, INV_TX_ID);  //Mailbox for Inverter CAN messages
  can.setMBFilter(MB1, CHG_TX_ID);  //Mailbox for Charger CAN Messages
  can.setMBFilter(MB2, 0x1806E5F4);  //Mailbox for Charger CAN Messages

  //Watchdog 
  if(watchdog_timeout != 0){                  //callback function is having some issues
    WDT_timings_t config; 
    int watchdog_trigger = watchdog_timeout - 1;
    if(watchdog_trigger < 1){
      watchdog_trigger = 1;
    }
    config.trigger = 11;   /* in seconds, 0->128 */    //time until watchdog callback function is triggered. 
    config.timeout = watchdog_timeout;               /* in seconds, 0->128 */   //time until watchdog reset
    config.pin = 20;                                //pin to be driven low upon reset. WDT1 holds low, WDT2 pulses low
    config.callback = myCallback;
    wdt.begin(config);              //This needs moved to the main loop
  }

  //Bring up ADC
  initialize_ADC();

    //current offset compensation
  measure_current();
  current_offset = current;

  //Bring up references on sense boards
  //configure_sense();

  check_memory();       //must be called to use SD card

//voltage poll and temperature poll take 16 and 24 milliseconds. The rest of the measure functions only take 1 or two milliseconds

  //flash_leds();

  if(mode == ""){
    measure_voltage();
    update_SOC();
    CAN_message_t msg;
    while(1){
      Serial.println("Setup");
      measure_current();
      measure_voltage();
      measure_temp();
      print_min_max();
      reset_watchdog();
      msg = RX_CAN();
      String input = Serial.readStringUntil('\n');
      input.trim();
      if(msg.id == INV_TX_ID){    //Always check msg id
        mode = "standy";
        //can.setMBFilter(MB1, 0);  //Disable Charger Mailbox
        break;
      }
      else if(msg.id == CHG_TX_ID){
        mode = "charge";
        //can.setMBFilter(MB0, 0);  //Disable Inverter Mailbox
        break;
      }
      else if(current >= 0.5){
        mode = "drive";
        break;
      }
      else if(input == "debug"){
        mode = "debug";
        break;
      }
      delay(20);
    }
  }
}

void loop() {
  if(mode == "charge"){
    Serial.println("Charge Mode Entered");
    CAN_message_t msg;
    float charger_voltage  = 0;
    float charger_current = 0;
    String filename = "data" + String(data_file_num) + ".csv";        //create data file
    File file = SD.open(filename.c_str(), FILE_WRITE);
    file.close();

    delay(6000);  //cause comm fault on charger. Power cycling the BMS without ensuring the charger fully powers down would otherwise can cause the BMS to enter the charge cycle agian.

    while(1){                   //precharge cycle
      measure_voltage();
      measure_temp();
      reset_watchdog();
      charger_enable(true);                 //send charge-disable message and clear comm fault on charger
      msg = RX_CAN();
      charger_voltage = ((uint16_t) msg.buf[0]<<8 | (uint16_t) msg.buf[1])/10;
      charger_current = ((uint16_t) msg.buf[2]<<8 | (uint16_t) msg.buf[3])/10;
      Serial.println(charger_voltage);
      Serial.println(pack_voltage);
      if(msg.id == CHG_TX_ID && msg.buf[4] == 0 && charger_voltage >= pack_voltage * 0.80){   //if can id matches charger and there are no charger faults AND precharge is complete
        break;
      }
    }
    //00100 low ac power on charger flag
    delay(1000);    //delay so that another Charger CAN message is sent to the BMS (so that an empty CAN buffer is not read which would raise a charger error)

    while(1){       //charge cycle
      Serial.print("charge fault status: "); Serial.println(charger_fault);
      measure_voltage();
      measure_temp();
      measure_current();
      Serial.print("current: ");  Serial.println(current);
      Serial.print("Pack Voltage: "); Serial.println(pack_voltage);
      if(!memory_fault){
        SD_data_write();
      }
      if(reset_watchdog()){
        msg = RX_CAN();
        charger_voltage = ((uint16_t) msg.buf[0]<<8 | (uint16_t) msg.buf[1])/10;
        charger_current = ((uint16_t) msg.buf[2]<<8 | (uint16_t) msg.buf[3])/10;
        Serial.print("charger voltage: "); Serial.println(charger_voltage);
        Serial.print("charger current: "); Serial.println(charger_current);
        if(msg.id == CHG_TX_ID && msg.buf[4] == 0 || true){
          charger_enable(false);   
        }
        else{                   //charger error
          digitalWrite(20, LOW);
          charger_enable(false);
          Serial.println("Charger Error");
          charger_fault = 1;
        }
      }
      delay(1000);
    }

    //charger fault
    while(1){
      Serial.println("Charger Fault");
      delay(1000);
    }
   
  }

  if(mode == "standby"){                  //waiting to drive. Still provides rules-compliant monitering in case CAN is lost
    Serial.println("Standby Mode Entered");

    if(!memory_fault){
    String filename = "data" + String(data_file_num) + ".csv";        //create data file
    File file = SD.open(filename.c_str(), FILE_WRITE);               
    file.close();
    SD_data_write();                                          //write initial conditions to data file once
    }

    while(1){
    Serial.println(mode);
    CAN_message_t msg;
    measure_voltage();
    measure_temp();
    measure_current();
    reset_watchdog();
    msg = RX_CAN();
    if(msg.id == INV_TX_ID){
      inv_voltage = float(msg.buf[0]*256 + msg.buf[1]);
    }
    if(inv_voltage >= pack_voltage * 0.8){    //checks inverter voltage to see if precharge is occuring
      mode = "drive";                         //enter drive mode if precharging
      break;
    }
    delay(10);  
    }
  }

  else if(mode == "drive"){
    Serial.println("Drive Mode Entered");
    int n = 0;    //time step number
  
    CAN_message_t msg;

    while(1){
      time_buffer[n] = millis() - start_time;
      if(n%current_interval == 0){
        measure_current();
        current_buffer[int(n/current_interval)] = current;
      }
      if(n%volt_interval == 0){
        measure_voltage();
        Serial.println("New volt");
        for(int i = 0; i<num_boards;i++){
          for(int j = 0; j<num_cells; j++){
            voltage_buffer[int(n/volt_interval)][i][j] = cell_voltage[i][j];
          }
        }
      }
      if(n%temp_interval == 0){
        Serial.println("New Temp");
        measure_temp();
        for(int i = 0; i<num_boards;i++){
          for(int j = 0; j<9; j++){
            temp_buffer[int(n/temp_interval)][i][j] = cell_temp[i][j];
          }
        }
      }
      if(new_voltage && new_temp){
        print_min_max();
        Serial.println("Reset_watchdog");
        reset_watchdog();
      }
      if(n%SD_interval == 0 && !memory_fault){
        SD_data_write();
      }

      // msg = RX_CAN();
      // if(msg.id == INV_TX_ID){
      //   inv_voltage = float(msg.buf[0]*256 + msg.buf[1]);
      // }
      // if(inv_voltage < pack_voltage * 0.5){    //checks inverter voltage to see if tractive system voltage is dropping
      //   mode = "standby";                         //enter standby mode if ready to drive is exited
      //   //data_file_num = 0;
      //   //check_memory();         //assign a new data file number in case RTD is entered agian
      //   break;
      // }

      while(millis() - start_time <= time_buffer[n] + time_step){     //this needs checked

      }
      if(n < SD_interval - 1){
      n++;
      }
      else{
        n = 0;
      }
      Serial.println(n);
    }
   
  }
  
  else{       //debug mode
    digitalWrite(20, LOW);                  //open shutdown circuit in debug mode
    Serial.println("Debug Mode Entered");
    while(1)
    dumpDataToSerial();
  }
 
}

void initialize_ADC(){
  //ADC sampling time constant (without external filter) = 50 ohms * 40 pF
  //CFR.B6 = 0 : uses external voltage reference
  //CFR.B9 = 0, FSR_ADC_A = 0 to VREF_A and FSR_ADC_B = 0 to VREF_B
  //CFR.B7 = 0 : single ended measurements
  //CFR.B11 = 0 and CFR.B10 = 1  Single-SDO Mode
  //CFR.B15:B11 = 1000 (write) or 0011 (read)
  uint8_t CFR_reg_MSB;
  uint8_t CFR_reg_LSB;
  uint8_t CFR_readback_MSB;                     
  uint8_t CFR_readback_LSB;                     
  CFR_reg_MSB = 0b10000100;  //CFR [B15:B8]
  //CFR_reg_LSB = 0b01000000;  //CFR [B7:B0]
  CFR_reg_LSB = 0b00000000;

  //Send write CFR register command
  digitalWrite(CS1, LOW);
  SPI1.transfer(CFR_reg_MSB);
  SPI1.transfer(CFR_reg_LSB);
  for(int i = 0; i<6; i++){   //clock ADC
    SPI1.transfer(0b00000000);
  }
  digitalWrite(CS1, HIGH);
  delay(2);

  //Send read CFR register command while clocking the write config
  digitalWrite(CS1, LOW);
  SPI1.transfer(0b00110000);
  for(int i = 0; i<6; i++){
    SPI1.transfer(0b00000000);    //clock ADC
  }
  digitalWrite(CS1, HIGH);
  delay(2);

  //readback the CFR configuration
  digitalWrite(CS1, LOW);
  CFR_readback_MSB = SPI1.transfer(0b00000000);
  CFR_readback_LSB = SPI1.transfer(0b00000000);
  for(int i = 0; i<6; i++){
    SPI1.transfer(0b00000000);  //clock ADC
  }
  digitalWrite(CS1, HIGH);

  //the 4 MSBs of the CFR register (read/write command bits) are cleared in Frame F+2 which is not consistant with the datasheet
  if((uint8_t)(CFR_reg_MSB<<4) != (uint8_t)(CFR_readback_MSB<<4) || (uint8_t)(CFR_reg_LSB<<4) != (uint8_t)(CFR_readback_LSB<<4)){   //bit-shifts to mask the 4 MSBs
    Serial.println("ADC_initialization ERROR");
    Serial.println((CFR_reg_MSB<<4), BIN);
    Serial.println((CFR_reg_LSB<<4), BIN);
    curr_sense_fault = 1;
  }
}

void read_ADC(){
  uint16_t ADC_A;
  uint16_t ADC_B;
  float A_volt;
  float B_volt;
  uint16_t temp;
  digitalWrite(CS1, LOW);
  for(int i = 0; i<2; i++){
    temp = SPI1.transfer(0b00000000); //clock ADC
    Serial.print("temp: "); Serial.println(temp, BIN);

  }
  ADC_A = SPI1.transfer(0b00000000);
  ADC_A = ADC_A << 8;
  ADC_A = ADC_A | SPI1.transfer(0b00000000);
  Serial.print("ADC_A: "); Serial.println(ADC_A);
  ADC_B = SPI1.transfer(0b00000000);
  ADC_B = ADC_B << 8;
  ADC_B = ADC_B | SPI1.transfer(0b00000000);
  Serial.print("ADC_B: "); Serial.println(ADC_B);
  temp = SPI1.transfer(0b00000000);
  Serial.print("temp: "); Serial.println(temp, BIN);
  digitalWrite(CS1, HIGH);
  delay(2);
  A_volt = (float)(ADC_A)/65535*5;
  B_volt = (float)(ADC_B)/65535*5;
  Serial.print("A Voltage: "); Serial.println(A_volt);
  Serial.print("B Voltage: "); Serial.println(B_volt);
}


void print_min_max(){   //This function prints the min and max parameters
  float min_cell_voltage = cell_voltage[0][0];
  float max_cell_voltage = cell_voltage[0][0];
  float min_cell_temp = cell_temp[0][0];
  float max_cell_temp = cell_temp[0][0];
  float min_die_temp = die_temps[0];
  float max_die_temp = die_temps[0];
  min_max<num_boards,num_cells>(cell_voltage, &min_cell_voltage, &max_cell_voltage);
  min_max<num_boards,9>(cell_temp, &min_cell_temp, &max_cell_temp);
  min_max<1, num_boards>(&die_temps, &min_die_temp, &max_die_temp);       //This is how you pass a 1D array to the min_max function
  Serial.print("Max cell voltage: "); Serial.println(max_cell_voltage);
  Serial.print("Min cell_voltage: "); Serial.println(min_cell_voltage);
  Serial.print("Max cell_temp: "); Serial.println(max_cell_temp);
  Serial.print("Min cell_temp: "); Serial.println(min_cell_temp);
  Serial.print("Max die temp: "); Serial.println(max_die_temp);
  Serial.print("Min die temp: "); Serial.println(min_die_temp);
}


void dumpDataToSerial() {
  while(1){
    String input = Serial.readStringUntil('\n');
    input.trim();
    if(input == "begin"){
      break;
    }
  }

  File root = SD.open("/");
  File entry = root.openNextFile();
  int file_num = 0;
  while (entry) {
      if(file_num >= file_read_begin){      //begin with file at file_read_begin
        Serial.println(entry.name());
        while (entry.available()) {
          char character = entry.read();
          Serial.write(character);

          while(character == '\n'){       //block until confirmation from Python that line has been read
            String input = Serial.readStringUntil('\n');
            input.trim();
            if(input == "next line"){
              break;
            }
          }
        }
        Serial.println("done");

        while(1){                        //block until Python is ready to read next file
          String input = Serial.readStringUntil('\n');
          input.trim();
          if(input == "next file"){
            break;
          }
        }
      }
      entry.close();
      entry = root.openNextFile();
      file_num++;
  }
  root.close();

  Serial.println("serial dump done");
}

void check_memory(){    //this should check all files
  if (!SD.begin(chipSelect)) {
    Serial.println("SD card initialization failed!");
    memory_fault = 1;
    return;
  }

  if(!SD.exists("state.txt")) {
    File file = SD.open("state.txt", FILE_WRITE);
    file.close();
  }

  File root = SD.open("/");
  File entry = root.openNextFile();
  uint64_t memory_usage = 0;
  int num_files = 0;
  while (entry) {
    num_files++;
    Serial.print(entry.name());
    Serial.print("\t");
    Serial.print(entry.size());
    Serial.println(" bytes");

    memory_usage += entry.size();
    entry.close();
    //SD.remove(entry.name());
    entry = root.openNextFile();
  }
  root.close();
  Serial.print("Memory Usage: "); Serial.print(100*memory_usage/(SD_card_size*1e9)); Serial.println("%\n");
  if(memory_usage > 0.9*(SD_card_size*1e9)){
    Serial.println("SD card over 90% full");
    memory_fault = 1;
    return;
  }
  if(data_file_num == 0){
    for(int i = 1; i < num_files + 100; i++){
      String filename = "data" + String(i) + ".csv";
      if(!SD.exists(filename.c_str())){
        data_file_num = i;
        break;
      }
    }
  }
}

void get_SOC(){       //SOC should be written in the state.txt file as: "SOC:100"

  if(SD.exists("state.txt")){
    File file = SD.open("state.txt", FILE_READ);
    if (file) {
      String line;
      while (file.available()) {
        line = file.readStringUntil('\n');
        Serial.println(line);
        int delim_index = line.indexOf(':');
        String name = line.substring(0, delim_index);
        String value = line.substring(delim_index + 1);
        map_text2var(name, value);
      }
      file.close();
    }
  }
  else {
    File file = SD.open("state.txt", FILE_WRITE);
    Serial.println("Initializing state of charge to 100%");
    file.close();
    Serial.println("state.txt initialized.");
  }
}

void map_text2var(String name, String value){     //map text name and value to a variable
  if(name == "SOC:"){
    soc = value.toFloat();
    Serial.println(soc);
  }
}

float update_SOC(){
  const int  discharge_curve_length = sizeof(discharge_points) / sizeof(discharge_points[0]);                              //length of each discharge curve
  const float max_capacity = discharge_points[0];                                                                           //maximum capacity of a single cell
  const int num_current_curves = sizeof(discharge_currents)/sizeof(discharge_currents[0]);      //number of discharge curves @ different currents

  float min_cell_voltage = cell_voltage[0][0];      //funct. min_max requires that the min and max values are initalized within the range of the min max values
  float max_cell_voltage = cell_voltage[0][0];
  min_max<num_boards,num_cells>(cell_voltage, &min_cell_voltage, &max_cell_voltage);
  float discharged = interpolate<discharge_curve_length>(discharge_curves[0], discharge_points, min_cell_voltage);    //capacity which has already been discharged (mAh)
  soc = 100 - ((max_capacity - discharged)/max_capacity * 100);
  Serial.print("SOC: ");  Serial.println(soc);
  return soc;
}

void upadate_current_limit(){
  const int  discharge_curve_length = sizeof(discharge_points) / sizeof(discharge_points[0]);                              //length of each discharge curve
  const float max_capacity = discharge_points[0];                                                                           //maximum capacity of a single cell
  const int num_current_curves = sizeof(discharge_currents)/sizeof(discharge_currents[0]);      //number of discharge curves @ different currents
}

void SD_data_write() {
  String filename = "data" + String(data_file_num) + ".csv";     
  File dataFile = SD.open(filename.c_str(), FILE_WRITE);
  if (dataFile) {
      dataFile.print("Mode: "); dataFile.println(mode);
      for(int n = 0; n<SD_interval; n++){
        dataFile.print("Voltage:\n");
        if(n % volt_interval == 0 || mode != "drive"){
          for (int i = 0; i < num_boards; i++) {
            for (int j = 0; j < num_cells; j++) {
              if(mode == "drive")
                dataFile.print(voltage_buffer[int(n/volt_interval)][i][j], 4);
              else
                dataFile.print(cell_voltage[i][j], 4);
              dataFile.print(", ");
            }
            dataFile.print("\n");
          }
        }
        if(n%temp_interval == 0 || mode != "drive"){
          dataFile.print("\nTemperature:\n");
          for (int i = 0; i < num_boards; i++) {
            for (int j = 0; j < 9; j++) {
              if(mode == "drive")
                dataFile.print(temp_buffer[int(n/temp_interval)][i][j], 2);
              else
                dataFile.print(cell_temp[i][j], 2);
              dataFile.print(", ");
            }
            dataFile.print("\n");
          }
        }
        if(n%current_interval == 0 || mode != "drive"){
        dataFile.print("Current: ");
        if(mode == "drive")
          dataFile.print(current_buffer[int(n/current_interval)]);
        else
          dataFile.print(current);
        dataFile.print("\n");
        }
        dataFile.print("Time:\n");

        //time stamp
        if(mode == "drive")
          dataFile.println(time_buffer[n]);
        else
          dataFile.println(millis() - start_time);
          break;
    }
    dataFile.close();
  }
}

void send_command(uint16_t command){
  uint8_t comm_arr[2];
  uint16_t pec;
  uint8_t pec0;
  uint8_t pec1;
  uint8_t cmd0;
  uint8_t cmd1;

  cmd0 = command >> 8;
  cmd1 = command >> 0;

  if(millis() - sense_watchdog_timer >= 1800){
  wakeup_sleep(num_boards + 1);
  sense_watchdog_timer = millis();
  }
  else{
  wakeup_idle(num_boards);
  sense_watchdog_timer = millis();
  }

  digitalWrite(CS, LOW);

  comm_arr[0] = cmd0;
  comm_arr[1] = cmd1;

  pec = pec15_calc(2, comm_arr);
  pec1 = pec >> 0;
  pec0 = pec >> 8;

  SPI.transfer(cmd0);
  SPI.transfer(cmd1);
  SPI.transfer(pec0);
  SPI.transfer(pec1);

}

void read_register_group(uint16_t command, uint8_t response[num_boards][6]){      //register group is always 6 bytes 

  uint16_t pec;
  uint8_t pec0;
  uint8_t pec1;
  uint8_t response_pec0;
  uint8_t response_pec1;

  send_command(command);

  for (int i = 0; i < num_boards; i++){
    for (int j = 0; j < 6; j++) {
      response[i][j] = SPI.transfer(0b11111111); // Send dummy byte to receive data
      //Serial.println(response[i][j], BIN); 
    }
    response_pec0 = SPI.transfer(0xFF);
    response_pec1 = SPI.transfer(0xFF);
    pec = pec15_calc(6, response[i]);
    pec1 = pec >> 0;
    pec0 = pec >> 8;
    
  if(response_pec0 != pec0 || response_pec1 != pec1){   //this recursion needs fixed
    Serial.println("pec error");
    wakeup_sleep(num_boards + 1);
    //read_register_group(command, response);
  }

  }

  pec = pec15_calc(6, response[0]);     //this needs fixed to include multiple boards

      // Serial.println("response pec");
      // Serial.println(response_pec0, BIN);
      // Serial.println(response_pec1, BIN);
      // Serial.println("calculated pec");
      // Serial.println(pec, BIN);

    digitalWrite(CS, HIGH);

}

void write_register_group(uint16_t command, uint8_t data[num_boards][6]){

  uint8_t return_data;
  uint8_t data_pec0;
  uint8_t data_pec1;
  uint16_t data_pec;

  send_command(command);

  for(int i = num_boards-1; i>=0; i--){
  data_pec = pec15_calc(6, data[i]);
  data_pec1 = data_pec >> 0;
  data_pec0 = data_pec >> 8;

  for(int j=0; j<6 ; j++){
    SPI.transfer(data[i][j]);
  }
    SPI.transfer(data_pec0);
    SPI.transfer(data_pec1);
  }
  digitalWrite(CS, HIGH);
}

void poll_ADC(uint16_t command, bool curr_measure){
  uint8_t return_data = 0;
  send_command(command);

  if(!curr_sense_fault && curr_measure){
  measure_current();
  }

  int num_polls = 0;
  while (return_data == 0) {                                                      //This needs a timeout condition
    return_data = SPI.transfer(0b11111111); // Send dummy byte to receive data
    num_polls++;
  }
  // Serial.println("ADC Conversion Done!");
  // Serial.println(num_polls);

  digitalWrite(CS, HIGH);
}

void measure_voltage(){     //18 millisecond execution time
  uint8_t response[num_boards][6];
  uint16_t cell_comm[6] = {RDCVA, RDCVB, RDCVC, RDCVD, RDCVE, RDCVF};   //read cell voltage registers A through E commands

  ////cell voltage measurement algorithm outlined in INTERNAL PROTECTION AND FILTERING section of LTC6813 datasheet////
  //poll_ADC(ADCV | 0b1);   //measure cells 1,7,13 to allow MUX voltage to settle
  //delay(cell_RC * 6);

  poll_ADC(ADCV);   //initiate and wait for voltage measurement

  for(int i=0; i*3 < num_cells; i++){         //i: cell group
    //Serial.print('i');
    //Serial.println(i);
    uint16_t curr_comm = cell_comm[i];                 //each command reads a sequential set of three cells from each board
    read_register_group(curr_comm, response);
    pack_voltage = 0;
    for(int j=0; j < num_boards; j++){        //j:board number
      //Serial.print('j');
      //Serial.println(j);
      for(int k=0; k < 3 && i*3+k < num_cells; k++){   //cell number within register group
      //Serial.print('k');
      //Serial.println(k);
        cell_voltage[j][i*3+k] = (float)(((uint8_t)response[j][k*2+1] << 8) | response[j][k*2]) * 0.0001;  //LSB represents 100 uV
        pack_voltage = pack_voltage + cell_voltage[j][i*3+k];
      }
    }
  }

  new_voltage = true;

  if(debug){
    Serial.println("Voltages:");
    int g = 0;
    for(int i=0; i<num_boards; i++){
      Serial.print("board: "); Serial.println(i+1);
      for(int j=0; j<num_cells; j++){
        Serial.print(cell_voltage[i][j]);   
        Serial.print(" ");
        g++; 
      }
      Serial.println("");
    }
  }
  
}

float map_temp(float V){  
  int const size = sizeof(NTC_LUT) / sizeof(NTC_LUT[0]);
  float R_bias = 10000;
  float V_ref = 3.00;

  if(V_ref == V){   //divide by zero case
    return -55;
  }

  float NTC_res = (V/V_ref*R_bias)/(1-V/V_ref);

  // int i = 0;
  // float dist = std::abs(NTC_res - NTC_LUT[0]);
  // for(i = 1; i<size; i++){
  //   float new_dist = std::abs(NTC_res - NTC_LUT[i]);
  //   if(new_dist < dist){
  //     dist = new_dist;
  //   }
  //   else{
  //     i--;
  //     break;
  //   }
  // }

  int i = search<size>(NTC_LUT, NTC_res);
  float temperature = float(i)/float(size)*(150+55)-55;
  //temperature = V;
  return(temperature);
}

void measure_temp(bool open_wire_check){        //25 millisecond execution time

  uint8_t response[num_boards][6];
  uint16_t aux_comm[4] = {RDAUXA, RDAUXB, RDAUXC, RDAUXD};   //read aux registers A through D commands
  int temp_num = 0;       //temperature reading index 0-8
  int command_num = 0;    //command index within aux_comm array

  if(open_wire_check == false){
    poll_ADC(ADAX);   //initiate and wait for GPIO measurement
  }
  else{
    poll_ADC(AXOW);
  }

  while(temp_num < 9){
    uint16_t curr_comm = aux_comm[command_num];                 //each command reads a sequential set of three GPIO from each board (RDAUXB is an exception with just 2 GPIO)
    read_register_group(curr_comm, response);
    for(int k = 0; k < 3 && temp_num < 9; k++){
      if(command_num == 1 && k>1){                              //register group B only contains 2 GPIO measurements
        continue;
      }
      for(int j = 0; j< num_boards; j++){                               //maximum of 3 GPIO per register group and 9 thermistors
              cell_temp[j][temp_num] = (float)(((uint8_t)response[j][k*2+1] << 8) | response[j][k*2]) * 0.0001;  //LSB represents 100 uV
              //Serial.println(cell_temp[j][temp_num]);
      }   
      temp_num++;
    }
  command_num++;
  }

  for(int i=0; i<num_boards; i++){
    for(int j=0; j<9; j++){
        cell_temp[i][j] = map_temp(cell_temp[i][j]);
    }
  }

  new_temp = true;

  if(debug){
    Serial.println("Tempearatures:");
    for(int i=0; i<num_boards; i++){
      Serial.print("board: "); Serial.println(i+1);
      for(int j=0; j<9; j++){
        Serial.print(cell_temp[i][j]);
        Serial.print(" ");
      }
      Serial.println("");
    }
  }
}

bool reset_watchdog(){      //this needs to clear the voltage and temperature measurements after reading them
  new_voltage = false;
  new_temp = false;

  for(int i = 0; i < num_boards; i++){
    for(int j = 0; j< num_cells; j++){
      if(cell_voltage[i][j] < OV && cell_voltage[i][j] > UV){
        cell_voltage[i][j] = 0;         
        continue;
      }
      else{
          digitalWrite(20, LOW);
          delay(1000);                    //delay to overcome debounce of shutdown circuit
          Serial.println("invalid voltage");
          Serial.println(cell_voltage[i][j]);
          return false;
      }
    }
  }

  for(int i = 0; i < num_boards; i++){
    for(int j = 0; j < 9; j++){
      if(cell_temp[i][j] > min_temp && cell_temp[i][j] < max_temp){
        cell_temp[i][j] = min_temp;
        continue;
      }
      else{
          digitalWrite(20, LOW);
          delay(1000);                //delay to overcome debounce of shutdown circuit
          Serial.print("invalid temp Board:  "); Serial.print(i+1); Serial.print("Num: "); Serial.println(j+1);
          return false;
      }
    }
  }
  digitalWrite(20, HIGH);
  wdt.feed();  
  //Serial.println("Watchdog fed");
  return true;
}

void measure_current(){
  uint16_t ADC;
  float volt;
  digitalWrite(CS1, LOW);

  for(int i = 0; i<2; i++){
    SPI1.transfer(0b00000000); //clock ADC
  }

  ADC = SPI1.transfer(0b00000000);
  ADC = ADC << 8;
  ADC = ADC | SPI1.transfer(0b00000000);
  volt = (float)(ADC)/65535*5;
  current = (volt-0.25)/(4.5)*(100)-50 - current_offset;   //this needs checked
  
  if(current > 50){                        //so does this
  ADC = SPI1.transfer(0b00000000);
  ADC = ADC << 8;
  ADC = ADC | SPI1.transfer(0b00000000);
  volt = (float)(ADC)/65535*5;
  current = (volt-0.25)/(4.5)*(100)-50 - current_offset;   //this needs checked
  }

  // if(debug){
  //   Serial.print("current: "); Serial.println(current);
  // }

  digitalWrite(CS1, HIGH);

}


void charger_enable(bool enable){
  digitalWrite(STBY, LOW);
  digitalWrite(CTX3, HIGH);
  delay(1);
  CAN_message_t CHGR_EN;
  //CHGR_EN.id = 0x1806E5F4;  // Set the CAN message ID     //datasheet
  CHGR_EN.id = 0x1806E5F4;  // Set the CAN message ID     //datasheet
  //CHGR_EN.id = 0x18FF50E5;  //charger send can id?
  CHGR_EN.flags.extended = 1; 
  CHGR_EN.len = 8;     // Set the data length
  //7FF max CAN ID

  uint16_t voltage_int = (uint16_t)(CHG_voltage * 10);
  uint16_t current_int = (uint16_t)(CHG_current * 10);
  
  
  CHGR_EN.buf[0] = (uint8_t)(voltage_int >> 8);           // High byte
  CHGR_EN.buf[1] = (uint8_t)(voltage_int);    // Low byte
  CHGR_EN.buf[2] = (uint8_t)(current_int >> 8);           // High byte
  CHGR_EN.buf[3] = (uint8_t)(current_int);    // Low byte
  CHGR_EN.buf[4] = (uint8_t)(enable);
  CHGR_EN.buf[5] = 0;
  CHGR_EN.buf[6] = 0;
  CHGR_EN.buf[7] = 0;
  
  bool message_sent = can.write(CHGR_EN);

  digitalWrite(CTX3, LOW);

  if(!message_sent){

  }

}

void TX_CAN(){
  float min_cell_voltage = cell_voltage[0][0];      //funct. min_max requires that the min and max values are initalized within the range of the min max values
  float max_cell_voltage = cell_voltage[0][0];
  float min_cell_temp = cell_temp[0][0];
  float max_cell_temp = cell_temp[0][0];
  min_max<num_boards,num_cells>(cell_voltage, &min_cell_voltage, &max_cell_voltage);
  min_max<num_boards,9>(cell_temp, &min_cell_temp, &max_cell_temp);

  digitalWrite(STBY, LOW);
  digitalWrite(CTX3, HIGH);
  delay(1);
  digitalWrite(CTX3, LOW);
  CAN_message_t BMS_data;
  //BMS_data.id = BMS_ID;
  BMS_data.id = BMS_ID;
  BMS_data.flags.extended = 0; 
  BMS_data.len = 8;     // Set the data length

  BMS_data.buf[0] = float_2_uint8_t(soc, 0, 100);               //SOC
  BMS_data.buf[1] = float_2_uint8_t(12, 0, 200);                 //current
  BMS_data.buf[2] = float_2_uint8_t(max_cell_voltage, 0, 5);    //max cell
  BMS_data.buf[3] = float_2_uint8_t(max_cell_temp, 0, 150);
  BMS_data.buf[4] = 0;
  BMS_data.buf[5] = 0;
  BMS_data.buf[6] = 0;
  BMS_data.buf[7] = 0;

  if(can.write(BMS_data)){
    Serial.println("CAN message sent");
  }
  else{
    Serial.println("CAN message TX Failed");
  }

}

uint8_t float_2_uint8_t(float float_val, float min, float max){   //float to uint8_t, clips values under/over min or max
  if(max == min) {return(0);}   //divide by zero risk
  if(float_val >= max){         //overflow risk
    return max;
  }
  if(float_val <= min){         //overflow risk
    return min;
  }
  uint8_t scaled = (uint8_t)(((float_val - min) / (max - min)) * 255.0);    //float decoded = ((float)scaled / 255.0) * (max - min) + min;
  return(scaled);
}

CAN_message_t RX_CAN(){     //grabs the first message in the FIFO. 
  static int curr_time = 0;

  //left bit in charger flag is highest bit (bit 4)
  CAN_message_t msg = {};
  digitalWrite(STBY, LOW);
  bool recieved = false;
  can.read(msg);
  //can.readMB(msg);
  if(msg.id != 0 && debug){
    Serial.print("ID: ");
    Serial.print(msg.id, HEX);
    Serial.println(" Data: ");
    //msg.len = 8;
    for (int i = 0; i < msg.len; i++) {
      Serial.print(msg.buf[i], BIN);
      Serial.print(" ");
    }
    Serial.print('\n');
  }
  return msg;   //always check the ID of the returned message. No messages in buffer returns 0 ID with 8 byte of zero data
}

void configure_sense(){     
  uint8_t data[6];        
  uint8_t data_arr[num_boards][6];  //contains identicle copies of data for each board
  uint16_t VUV;
  uint16_t VOV;
  VUV = UV/(16*0.0001)-1;     //Comparison Voltage = (VUV + 1) • 16 • 100μV  (pg. 68 in datasheet)
  VOV = OV/(16*0.0001);       //Comparison Voltage = VOV • 16 • 100μV        (pg. 68 in datasheet)

  Serial.println(VUV, BIN);
  Serial.println(VOV, BIN);

  data[0] = 0b11111100;     //GPIO1-5 = 1 (pull-down off), REFON=1, DTEN=0, ADCOPT=0
  data[1] = (uint8_t) VUV;
  data[2] = (uint8_t) (VOV & 0b11110000) | (VUV>>8 & 0b00001111);
  data[3] = (uint8_t) VOV>>4;
  data[4] = 0b00000000;
  data[5] = 0b00000000;

  // data[0] = 0b11111110;     //GPIO1-5 = 1 (pull-down off), REFON=1, DTEN=0, ADCOPT=0
  // data[1] = (uint8_t) VUV;
  // data[2] = (uint8_t) (VOV & 0b11110000) | (VUV>>8 & 0b00001111);
  // data[3] = (uint8_t) VOV>>4;
  // data[4] = 0b11111111;
  // data[5] = 0b11111111;

  for(int i = 0; i< num_boards; i++){
    std::copy(data, data + 6, data_arr[i]);
  }
  write_register_group(WRCFGA, data_arr);
}

void balance(bool keep_going){
  bool discharge[num_boards][18] = {0};  //'1': needs dischaged, '0': does not need discharged
  float min = cell_voltage[0][0];
  float max = cell_voltage[0][0];

  sense_status();
  ////mark cells to be discharged////
  min_max<num_boards,num_cells>(cell_voltage, &min, &max);
  Serial.print("min cell voltage: "); Serial.println(min);
  Serial.print("max cell voltage: "); Serial.println(max);
  Serial.println("Cells to be discharged");
  if(keep_going){
    for(int i = 0; i<num_boards; i++){
      for(int j =0; j<num_cells; j++){
          discharge[i][j] = cell_voltage[i][j] > min && cell_voltage[i][j] > balance_threshold && die_temps[i] < 60.0f;
          if(discharge[i][j]){
          Serial.print("Board: "); Serial.print(i+1); Serial.print("  Cell: "); Serial.print(j+1); Serial.print(" Volt: "); Serial.println(cell_voltage[i][j]);
          Serial.print("die temp: "); Serial.println(die_temps[i]);
          }
      }
    }
  }
  discharge_cells(discharge);
  if(balance_threshold < min){
    balance_threshold = min;
  }
}

void sense_status(){              //really should be the measure die temp function
  uint8_t response[num_boards][6];
  poll_ADC(ADSTAT);
  read_register_group(RDSTATA, response);
  Serial.println("die_temps");
  for(int i = 0; i<num_boards;i++){
    die_temps[i]= (response[i][2] | response[i][3]<<8) * (0.0001/.0076) - 276;
    Serial.println(die_temps[i]);
  }
  Serial.println();
}

// void sense_status(){
//   uint8_t response[num_boards][6];
//   read_register_group(RDSTATB , response);

//   undervoltage_flag[0] = response[2]>>0 & 0b1;
//   undervoltage_flag[1] = response[2]>>2 & 0b1;
//   undervoltage_flag[2] = response[2]>>4 & 0b1;
//   undervoltage_flag[3] = response[2]>>6 & 0b1;
//   undervoltage_flag[4] = response[3]>>0 & 0b1;
//   undervoltage_flag[5] = response[3]>>2 & 0b1;
//   undervoltage_flag[6] = 0;
//   undervoltage_flag[7] = 0;
//   undervoltage_flag[8] = 0;
//   undervoltage_flag[9] = 0;
//   undervoltage_flag[10] = 0;
//   undervoltage_flag[11] = 0;
//   undervoltage_flag[12] = 0;
//   undervoltage_flag[13] = 0;
//   undervoltage_flag[14] = 0;
//   undervoltage_flag[15] = 0;
//   Serial.println("voltage flags");
//   for(int i = 0; i<=5; i++){
//     Serial.println(undervoltage_flag[i]);
//   }
//   Serial.println("done");

// }

void flash_leds(){                        //Flashes each discharge resistor sequentially
  int time_on =1000;                        //Time each led is on in milliseconds
  bool discharge[num_boards][18] = {0};  //'1': needs dischaged, '0': does not need discharged
  for(int i = num_boards; i>=0; i--){
    if(i % 4 < 2){
      for(int j = 0; j<num_cells;j++){
        discharge[i][j] = true;
        discharge_cells(discharge);
        delay(time_on);
        discharge[i][j] = false;
        discharge_cells(discharge);
      }
    }
    else{
      for(int j = num_cells; j>=0;j--){
        discharge[i][j] = true;
        discharge_cells(discharge);
        delay(time_on);
        discharge[i][j] = false;
        discharge_cells(discharge);
      }
    }
  }
}

void discharge_cells(bool discharge[num_boards][18]){      //this function takes a 2D boolean array which is NOT dependent on num_cells.
  uint8_t data[6];
  uint8_t data_arr[num_boards][6];
  uint16_t VUV;
  uint16_t VOV;
  VUV = UV/(16*0.0001)-1;     //Comparison Voltage = (VUV + 1) • 16 • 100μV  (pg. 68 in datasheet)
  VOV = OV/(16*0.0001);       //Comparison Voltage = VOV • 16 • 100μV        (pg. 68 in datasheet)
  ////configuration register group A////
  for(int i = 0; i< num_boards; i++){
    data[0] = 0b11111100;     //GPIO1-5 = 1 (pull-down off), REFON=1, DTEN=0, ADCOPT=0
    data[1] = (uint8_t) VUV;
    data[2] = (uint8_t) (VOV & 0b11110000) | (VUV>>8 & 0b00001111);
    data[3] = (uint8_t) VOV>>4;
    data[4] = (uint8_t) discharge[i][7]<<7 | discharge[i][6]<<6 | discharge[i][5]<<5 | discharge[i][4]<<4 | discharge[i][3]<<3 | discharge[i][2]<<2 | discharge[i][1]<<1 | discharge[i][0]<<0;
    data[5] = (uint8_t) discharge[i][11]<<3 | discharge[i][10]<<2 | discharge[i][9]<<1 | discharge[i][8]<<0;
    std::copy(data, data + 6, data_arr[i]);
  }

  write_register_group(WRCFGA, data_arr);
  ////configuration register group B/////
    for(int i = 0; i< num_boards; i++){
    data[0] = (uint8_t) discharge[i][15]<<7 | discharge[i][14]<<6 | discharge[i][13]<<5 |discharge[i][12]<<4 | 0b1111;
    data[1] = (uint8_t) discharge[i][17] | discharge[i][16];
    data[2] = (uint8_t) 0b00000000;
    data[3] = (uint8_t) 0b00000000;
    data[4] = (uint8_t) 0b00000000;
    data[5] = (uint8_t) 0b00000000;

    std::copy(data, data + 6, data_arr[i]);
  }
  write_register_group(WRCFGB, data_arr);
}

void myCallback() {    
  Serial.println("Callback called");
  measure_voltage();
  measure_temp();
  reset_watchdog();
  watchdog_callback = true;   //set watchdog callback flag
}


//Analog Devices provided Functions//

void wakeup_sleep(uint8_t total_ic) //Number of ICs in the system. This function needs some work. Enters Sleep state after 2 seconds of no command sent with valid PEC
{
  Serial.println("Wakeup Sleep");
	for (int i =0; i<total_ic; i++)
	{
	   digitalWrite(CS, LOW);
     delayMicroseconds(300);
	   digitalWrite(CS, HIGH);
     delayMicroseconds(10);
	}
}

uint16_t pec15_calc(uint8_t len, //Number of bytes that will be used to calculate a PEC
                    uint8_t *data //Array of data that will be used to calculate  a PEC
                   )
{
	uint16_t remainder,addr;
	remainder = 16;//initialize the PEC
	
	for (uint8_t i = 0; i<len; i++) // loops for each byte in data array
	{
		addr = ((remainder>>7)^data[i])&0xff;//calculate PEC table address
		#ifdef MBED
			remainder = (remainder<<8)^crc15Table[addr];
		#else
			remainder = (remainder<<8)^pgm_read_word_near(crc15Table+addr);
		#endif
	}
	
	return(remainder*2);//The CRC15 has a 0 in the LSB so the remainder must be multiplied by 2
}

void wakeup_idle(uint8_t total_ic){ //idle after 4.3 ms of no isoSPI activity
  //Serial.println("wakeup_idle");
	for (int i =0; i<total_ic + 1; i++){    //+1 IC for the LTC6820
  digitalWrite(CS, LOW);
  SPI.transfer(0b11111111);     //Guarantees the isoSPI will be in ready mode
  digitalWrite(CS, HIGH);
	}
  delayMicroseconds(1);   //This delay is absolutely needed: t5 in datasheet - CSB Rising Edge to CSB Falling Edge >= 0.65us
}
